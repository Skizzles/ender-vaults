plugins {
    id 'com.gradleup.shadow' version '8.3.1'
}

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
    maven { url 'https://repo.codemc.org/repository/nms/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation project(':api')

    compileOnly('org.spigotmc:spigot:1.21.1-R0.1-SNAPSHOT') {
        exclude group: 'com.mojang'
    }

    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.2.0'
    implementation 'com.saicone.rtag:rtag:1.5.6'
    implementation 'com.saicone.rtag:rtag-item:1.5.6'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand(rootProject.properties)
    }
}

shadowJar {
    archiveClassifier.set('')

    relocate 'com.zaxxer.hikari', project.group + '.lib.hikari'
    relocate 'org.mariadb.jdbc', project.group + '.lib.mariadb'
    relocate 'com.saicone.rtag', project.group + '.lib.rtag'
    relocate 'org.javatuples', project.group + '.lib.javatuples'

    minimize {
        exclude(dependency('org.mariadb.jdbc:mariadb-java-client:.*'))
    }
}

jar {
    enabled = false
    dependsOn (shadowJar)
}